/*
 * Copyright 2018 Roberto Leinardi.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply from: rootProject.file('checkstyle.gradle')
apply plugin: 'net.ltgt.errorprone'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    apply from: 'mavenConfig.gradle'

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.version_code
        versionName build_versions.version_name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    compileOptions {
        sourceCompatibility build_versions.java_version
        targetCompatibility build_versions.java_version
    }
}

dependencies {
    api deps.support.app_compat
    api deps.support.design
    api deps.support.cardview
    errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"
    errorprone "com.google.errorprone:error_prone_core:2.29.2"
    annotationProcessor "com.uber.nullaway:nullaway:0.10.20"
    annotationProcessor "edu.ucr.cs.riple.annotator:annotator-scanner:1.3.13-java-8-SNAPSHOT"
}

def scanner_config = "${rootProject.projectDir}/annotator-out/scanner.xml"
def nullaway_config = "${rootProject.projectDir}/annotator-out/checker.xml"

import net.ltgt.gradle.errorprone.CheckSeverity
tasks.withType(JavaCompile) {
    if (!name.toLowerCase().contains("test")) {
        println(scanner_config)
        println(nullaway_config)
        options.errorprone {
            check("NullAway", CheckSeverity.ERROR)
            check("AnnotatorScanner", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "com.leinardi.android.speeddial")
            option("NullAway:SerializeFixMetadata", "true")
            option("NullAway:FixSerializationConfigPath", nullaway_config)
            option("AnnotatorScanner:ConfigPath", scanner_config)
        }
  }
}
